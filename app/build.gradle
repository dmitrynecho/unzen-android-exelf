plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}
apply from: 'inspect-files.gradle'
apply from: 'zen-pack-elfs-app.gradle'
apply from: '../lib-cho/zen-tasks-graph.gradle'

android {
    namespace 'droid.elfexec'
    ndkVersion exelfNdkVersion
    defaultConfig {
        applicationId "droid.elfexec"
        minSdkVersion exelfMinSdkVersion
        compileSdk exelfCompileSdkVersion
        targetSdkVersion exelfTargetSdkVersion
        versionCode versionBase
        versionName "$versionBase"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
        resValue("string", "version_base", "${versionBase}")
        resValue("string", "build_time", "${LocalDateTime.now()}")
        resourceConfigurations += ['en']
    }
    buildTypes {
        debug {
            versionNameSuffix "D"
            minifyEnabled false
        }
        release {
            versionNameSuffix "R"
            minifyEnabled false
            ndk.debugSymbolLevel = 'none'
        }
    }

    //noinspection GrDeprecatedAPIUsage TAG3
    flavorDimensions "abi"

    productFlavors {
        x64 {
            versionCode 4 + android.defaultConfig.versionCode
            versionName "$versionCode+X64"
            ndk {
                abiFilters "x86_64"
            }
            ext {
                abis = ["x86_64"]
            }
        }
        x32 {
            versionCode 3 + android.defaultConfig.versionCode
            versionName "$versionCode+X32"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "x86"
            }
            ext {
                abis = ["x86"]
            }
        }
        a64 {
            versionCode 2 + android.defaultConfig.versionCode
            versionName "$versionCode+A64"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
            ext {
                abis = ["arm64-v8a"]
            }
        }
        a32 {
            versionCode 1 + android.defaultConfig.versionCode
            versionName "$versionCode+A32"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a"
            }
            ext {
                abis = ["armeabi-v7a"]
            }
        }
        fat {
            versionCode android.defaultConfig.versionCode
            versionName "$versionCode+FAT"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters = baseAbis
            }
            ext {
                abis = baseAbis
            }
        }
    }

    //noinspection GrDeprecatedAPIUsage TAG4
    variantFilter { variant ->
        ArrayList<String> names = variant.flavors*.name
        if (variant.buildType.name != "release" && !names.contains("fat")) {
            setIgnore(true)
        }
        if (releaseBundle) {
            if (!names.contains("fat")) {
                setIgnore(true)
            }
            List<String> variants = releaseBundleVariants
            Boolean found = false
            variants.forEach { String bt ->
                if (names.contains(bt)) {
                    found = true
                }
            }
            found = found || variants.contains(variant.buildType.name)
            if (!found) {
                setIgnore(true)
            }
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

addUninstallAllShortcut(project)

android.applicationVariants.configureEach { variant ->
    variant.outputs.configureEach { output ->
        outputFileName = "droid-elfexec-${variant.versionName}.apk"
    }
    variant.resValue("string", "version_code", "${variant.versionCode}")
    variant.resValue("string", "version_name", "${variant.versionName}")
    variant.resValue("string", "flavor_name", "${variant.flavorName}")
}

tasks.register('prebuildCleanup') {
    doLast {
        println("Build starts...")
    }
}
preBuild.dependsOn prebuildCleanup

tasks.register("makeBundlesMakeSplitApksInstallRun") {
    group = "aleph"
    description = "Public App Bundles build."
    doLast {
        file("build/outputs/bundle").listFiles().each { File dir ->
            Boolean enabledVariant = false
            releaseBundleVariants.forEach {
                if (dir.name.toLowerCase(Locale.US).contains(it)) {
                    enabledVariant = true
                }
            }
            if (enabledVariant) {
                copy {
                    from dir
                    include "*.aab"
                    into zenPackReleaseDir
                }
            }
        }
        file(zenPackReleaseDir).listFiles().each { File aab ->
            if (!aab.name.endsWith(".aab")) return
            String path = aab.absolutePath
            File apksFile = file(path.replace(".aab", ".apks"))
            apksFile.delete()
            bundlesToApks(path, null, false, false)
            installSplitApks(apksFile.absolutePath)
        }
        runApk(project, "droid.elfexec", "droid.elfexec.MainActivity")
    }
    dependsOn tasks.named(zenPackBundlesTaskName)
}
tasks.register("makeAllApksUnzipped") {
    group = "aleph-test"
    doLast {
        String path = zenPackReleaseDir
        fileTree(path).each { File aab ->
            if (!aab.name.endsWith(".aab")) return
            String outDir = aab.parent + "/" + aab.name.replace(".aab", "")
            String apksPath = aab.absolutePath.replace('.aab', '.apks')
            file(apksPath).delete()
            bundlesToApks(aab.absolutePath, null, true, false)
            copy {
                from zipTree(apksPath)
                include "*.apk"
                into outDir
            }
            file(apksPath).delete()
            bundlesToApks(aab.absolutePath, null, false, false)
            copy {
                from zipTree(apksPath)
                into outDir
                // Using "include "*.apk"" causes problems, possible
                // because of path inside ZIP.
            }
            file(apksPath).delete()
        }
        fileTree(path).each { File f ->
            if (!f.name.endsWith(".aab")
                    && !f.name.endsWith(".apk")
                    && !f.name.endsWith(".apks")) {
                f.delete()
            }
        }
    }
}
dependencies {
    implementation project(':lib-cuscuta')
    implementation project(':lib-cho')
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
}