apply plugin: 'com.android.application'

apply from: 'build-utils.gradle'

android {
    namespace 'unzen.exelf'
    ndkVersion exelfNdkVersion
    defaultConfig {
        applicationId "unzen.exelf"
        minSdkVersion 16
        compileSdk exelfCompileSdkVersion
        targetSdkVersion exelfTargetSdkVersion
        versionCode baseVersion
        versionName "$baseVersion"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
        buildConfigField("int", "BASE_VERSION_CODE", "$baseVersion")
    }
    buildTypes {
        debug {
            versionNameSuffix "D"
            minifyEnabled false
        }
        release {
            versionNameSuffix "R"
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // Warning is inconsistent with current official docs and fixing this
    // warning creates other errors.
    // - https://developer.android.com/build/build-variants#product-flavors
    //noinspection GrDeprecatedAPIUsage
    flavorDimensions "abi"

    productFlavors {
        x64 {
            versionCode 4 + android.defaultConfig.versionCode
            versionName "$versionCode+X64"
            ndk {
                abiFilters "x86_64"
            }
            ext {
                abis = ["x86_64"]
            }
        }
        x32 {
            versionCode 3 + android.defaultConfig.versionCode
            versionName "$versionCode+X32"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "x86"
            }
            ext {
                abis = ["x86"]
            }
        }
        a64 {
            versionCode 2 + android.defaultConfig.versionCode
            versionName "$versionCode+A64"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
            ext {
                abis = ["arm64-v8a"]
            }
        }
        a32 {
            versionCode 1 + android.defaultConfig.versionCode
            versionName "$versionCode+A32"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a"
            }
            ext {
                abis = ["armeabi-v7a"]
            }
        }
        fat {
            versionCode android.defaultConfig.versionCode
            versionName "$versionCode+FAT"
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters = baseAbis
            }
            ext {
                abis = baseAbis
            }
        }
    }
    // Warning is inconsistent with current official docs.
    // - https://developer.android.com/build/build-variants#filter-variants
    //noinspection GrDeprecatedAPIUsage
    variantFilter { variant ->
        ArrayList<String> names = variant.flavors*.name
        if (variant.buildType.name != "release" && !names.contains("fat")) {
            setIgnore(true)
        }
    }
}

android.applicationVariants.configureEach { variant ->
    variant.outputs.configureEach { output ->
        outputFileName = "unzen-exelf-${variant.versionName}.apk"
    }
}

tasks.register('alephCopyApks', Copy) {
    group = "aleph"
    from "build/outputs/apk/fat/debug"
    from "build/outputs/apk/fat/release"
    from "build/outputs/apk/a32/release"
    from "build/outputs/apk/a64/release"
    from "build/outputs/apk/x32/release"
    from "build/outputs/apk/x64/release"
    include "*.apk"
    into "$rootDir.absolutePath/apks"
}
assemble.finalizedBy alephCopyApks

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':lib-cuscuta')
    implementation 'androidx.annotation:annotation:1.8.2'
    testImplementation 'junit:junit:4.13.2'
    //noinspection GradleDependency - update requires minSdkVersion 19
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    //noinspection GradleDependency - update requires minSdkVersion 19
    androidTestImplementation 'androidx.test:runner:1.5.2'
}
