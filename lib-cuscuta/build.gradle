apply plugin: 'com.android.library'

android {
    namespace 'unzen.exelf.cuscuta'
    ndkVersion exelfNdkVersion
    defaultConfig {
        minSdkVersion 16
        compileSdk exelfCompileSdkVersion
        targetSdkVersion exelfTargetSdkVersion
        externalNativeBuild {
            ndkBuild {
                targets 'jnifoo', 'exebar', 'exebaz'
            }
        }
    }
    buildTypes {
        debug {
            jniDebuggable true
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters = baseAbis
            }
        }
        release {
            jniDebuggable false
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters = baseAbis
            }
        }
    }
    externalNativeBuild {
        ndkBuild {
            path "src/main/cpp/Android.mk"
        }
    }
}

android.libraryVariants.configureEach { variant ->
    // AGP gather solibs from all modules in merged_native_libs dir of app module and then
    // performs stripping on them that places stripped solibs in stripped_native_libs.
    // You can see this by search in task execution log for forementioned dirs and
    // see that stripping task runs after merge task. In AGP 7.3.1, when project contains
    // modules, stripping performed only once in app module, modules that app depends on
    // no more perform redurant solib stripping.
    def variantName = variant.name.capitalize()
    def intermedDirPath = "$project.buildDir.absolutePath/intermediates"
    // <module>/build/intermediates/ndkBuild/debug/obj/local
    def objDirPath = "$intermedDirPath/ndkBuild/$variant.dirName/obj/local"
    // <module>/build/intermediates/merged_native_libs/debug/out/lib
    def mergedDirPath = "$intermedDirPath/merged_native_libs/$variant.dirName/out/lib"

    def copyExesTaskName = "unzenCopyExesForStrip${variantName}"
    def copyExesTask = tasks.register(copyExesTaskName, Copy) {
        from objDirPath
        into mergedDirPath
        baseAbis.each { abi ->
            // Android Gradle builder is ignoring subdirs inside ABI dirs, so don't use them.
            include "$abi/exebar"
            include "$abi/exebaz"
        }
        // On debug app builds Android OS installs from APK arbitrary named binaries,
        // but on release build it ignores everything except files named like solibs.
        rename '(.+)', 'lib$1.so'
        includeEmptyDirs = false
    }
    tasks.named("merge${variantName}NativeLibs") {
        finalizedBy(copyExesTask)
    }
    // Have no idea what "copy${variantName}JniLibsProjectOnly" is doing, but assume next
    // dependency based on intuition to resolve warning "Execution optimizations have been
    // disabled for task ':lib-cuscuta:copyDebugJniLibsProjectOnly' to ensure correctness
    // due to the following reasons: Task ':lib-cuscuta:copyDebugJniLibsProjectOnly' uses
    // this output of task ':lib-cuscuta:unzenCopyExesForStripDebug' without declaring an
    // explicit or implicit dependency. This can lead to incorrect results being produced,
    // depending on what order the tasks are executed".
    tasks.named("copy${variantName}JniLibsProjectOnly") {
        dependsOn(copyExesTask)
    }
}

tasks.register('cppVersions') {
    File header = file('src/main/cpp/global_version.h')
    inputs.property('globalVersion', baseVersion)
    outputs.file header
    doLast {
        String template = "#ifndef %s%n#define %s%n%n#define %s \"%s\"%n%n#endif // %s"
        def name = "GLOBAL_VERSION"
        def guard = "_GLOBAS_VERSION_H_"
        def version = 'UNZEN-VERSION-' + baseVersion
        header.write(String.format(template, guard, guard, name, version, guard))
    }
}
preBuild.dependsOn cppVersions
