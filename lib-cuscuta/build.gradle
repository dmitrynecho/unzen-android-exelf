plugins {
    id 'com.android.library'
    alias(libs.plugins.kotlin.android)
}
apply from: 'zen-pack-elfs-lib.gradle'

android {
    namespace 'droid.elfexec.cuscuta'
    ndkVersion exelfNdkVersion
    defaultConfig {
        minSdkVersion exelfMinSdkVersion
        compileSdk exelfCompileSdkVersion
        targetSdkVersion exelfTargetSdkVersion
        externalNativeBuild {
            ndkBuild {
                targets = zenPackSosList + zenPackElfsList
            }
        }
    }
    buildTypes {
        debug {
            jniDebuggable true
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters = baseAbis
            }
        }
        release {
            jniDebuggable false
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters = baseAbis
            }
        }
    }
    externalNativeBuild {
        ndkBuild {
            path "src/main/cpp/Android.mk"
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

tasks.register('cppVersions') {
    String template = "#ifndef %s%n#define %s%n%n#define %s \"%s\"%n%n#endif // %s"
    File header = file('src/main/cpp/global_version.h')
    inputs.property('globalVersion', versionBase)
    outputs.file header
    doLast {
        String name = "CUSCUTA_GLOBAL_VERSION"
        String guard = "CUSCUTA_GLOBAL_VERSION_H_"
        String version = 'ELFEXEC-VERSION-' + versionBase
        header.write(String.format(template, guard, guard, name, version, guard))
    }
}
preBuild.dependsOn cppVersions